// 
//	L4 spiny stellate neuron simulations
//
//	Adapted from Lavzin et al., Nature 2012
//
//	Downloaded from ModelDB:	2014.07.22
// 	Modified by: 				Renaud Jolivet
// 	Last modification:			2015.05.22
// 				

// initial definitions
strdef 		seedasastring, gainasastring, fname, prefix
objectvar 	outfile	
objref 		gains, pc, myloc_pos, myloc_name
outfile 	    = new File()
gains 			= new Vector()	
myloc_pos 	    = new Vector()	
myloc_name      = new SectionList()
pc 				= new ParallelContext()
    
// scenarios
switch_plot     = 1
switch_print    = 1         
if (name_declared("scen")==5) {     // defines the default scenario
    scenario = scen
} else {
    scenario = 1
}
if ( scenario == 1 ) {              // complete simulation scaling all thalamocortical inputs
    switch_soma     = 0
    switch_scaling  = 1
    ge              = 0.0008 // µS
    gi              = 0.0005 // µS
    gtc             = 0.0010 // µS
    gms             = 0.0010 // µS
    prefix          = "std"
}
if ( scenario == 2 ) {              // complete simulation scaling only the input of interest    
    switch_soma     = 0
    switch_scaling  = 0
    ge              = 0.0008 // µS
    gi              = 0.0005 // µS
    gtc             = 0.0010 // µS
    gms             = 0.0010 // µS
    prefix          = "unscaled"
}
if ( scenario == 3 ) {              // simulation without inhibition scaling all thalamocortical inputs
    switch_soma     = 0
    switch_scaling  = 1
    ge              = 0.0008 // µS
    gi              = 0      // µS
    gtc             = 0.0010 // µS
    gms             = 0.0010 // µS
    prefix          = "woinh"
}
if ( scenario == 4 ) {              // one effective synapse at the soma
    switch_soma     = 2
    switch_scaling  = 1
    ge              = 0.0008 // µS
    gi              = 0      // µS
    gtc             = 0.0010 // µS
    gms             = 0.0010 // µS
    prefix          = "unique"
}

//
// 	Parameters that control the simulation are:
//		1. The seed of the RNG that controls the background noise;
//		2. The location of the thalamocortical synapses;
//		3. The amplification gain factor.
//	(see runSimulations.hoc)
//	 
rngseed 		= 1789				                                // 1. Seed of the RNG
gains.append(1, 0.75, 1.25, 1.5, 2, 3, 0.5, 0, 6, 9, 12)            // 3. List of gains to be tested	
    
// permutation of the gains
if (name_declared("perm")==5) {
	if (perm<gains.size()) {
        for ii = 1, perm {
		    gains.insrt(0,gains.x[gains.size()-1])
		    gains.remove(gains.size()-1)	
	    }
    } else {
	    rngseed = rngseed+perm
	}
}	

// init
load_file("nrngui.hoc")
vinit			= -70
dt				= 0.025 
steps_per_ms 	= 10
tstart 			= 0
tstop 			= 1000//130000
celsius         = 37 

// load cell morphology
load_file("l4sscell.hoc")
		
// define conductances
taus_hh3 		= 30
tausv_hh3		= 10
tausn_hh3		= 5
tausb_hh3		= 0.1
taun_hh3		= 2
taun2_hh3		= 40
tauh_hh3		= 1
    
soma_na			= 0.300
soma_k1			= 0.030
soma_k2 		= 0.100
dend_na			= 0.003
dend_k1			= 0.001
dend_k2			= 0.001
cabar			= 0.000003
					
// create cell
objref cell
cell 			= new l4(0,0)
 
// thalamocortical contacts
//myloc_pos.append(0.5,0.2,0.9)		                                // dataset position #1 
myloc_pos.append(0.9,0.15,0.1)		                                // dataset position #2
ntc             = myloc_pos.size()      

// dataset positions #1
//access cell.a3_122                                                  // 2. Dendrites where to place the thalamocortical synapse
//myloc_name.append()
//access cell.a3_121                                         
//myloc_name.append()
//access cell.a3_12                                         
//myloc_name.append()

// positions #2
access cell.a5_1111                                                 // 2. Dendrites where to place the thalamocortical synapse
myloc_name.append()
access cell.a5_11111                                       
myloc_name.append()
access cell.a5_11111                                         
myloc_name.append()
    
// procedures to set segments and distribute channels
proc geom_nseg() {
	forall { nseg = int((L/(0.1*lambda_f(100))+.999)/2)*2+1 }
}				
geom_nseg()
proc init_active() {
    access cell.soma
	insert hh3
    insert cah
    insert cad
	vshift_hh3 	= -10
	gnabar_hh3 	= soma_na
	gkbar_hh3 	= soma_k1
	gkbar2_hh3	= soma_k2
	gl_hh3		= 0
	gbar_cah	= cabar    
    ena         = 70    
    v			= vinit 
	forsec cell.all{
	    insert hh3
		insert cad
		insert cah
		vshift_hh3	= -10
        gnabar_hh3	= dend_na
		gkbar_hh3	= dend_k1
		gkbar2_hh3	= dend_k2
		gl_hh3		= 0
		gbar_cah	= cabar
        ena         = 70   
        v			= vinit 
	}
}
init_active()
cell.soma distance()                                                // set cell.soma(0) as the origin for distance measurements
    
// record voltage at the soma
objref savevec
savevec			= new Vector()
savevec.record(&cell.soma.v(.5))    
	
// procedure to read currents
objref vec_sodium, vec_calcium, vec_ex, vec_tc, vec_ms
itot_na 		= 0
itot_ca 		= 0
itot_ex         = 0
itot_tc         = 0
itot_ms         = 0
vec_sodium		= new Vector()
vec_calcium		= new Vector()
vec_ex			= new Vector()
vec_tc			= new Vector()
vec_ms			= new Vector()
vec_sodium.record(&itot_na)
vec_calcium.record(&itot_ca)
vec_ex.record(&itot_ex)
vec_tc.record(&itot_tc)
vec_ms.record(&itot_ms)
proc read_compartment_currents() {
    if (ismembrane("na_ion")) { 
		ina_comp 	= ina($1)*1e-2*area($1) 
		itot_na 	= itot_na+ina_comp
    }
    if (ismembrane("ca_ion")) { 
		ica_comp 	= ica($1)*1e-2*area($1) 
		itot_ca 	= itot_ca+ica_comp
    }
}
		
// AP count
objectvar myapc, stvector
stvector 		= new Vector()
myapc 			= new APCount(0.5)
myapc.thresh	= -30
myapc.record(stvector)
	
// create graph to display the voltage at the soma
objref g, pwm
pwm 			= new PWManager()                               // to manage windows
if (switch_plot == 1) {  
    g 			= new Graph()	      
    addplot(g,0)                                                // add 'g' to the list of stuff updated at each iteration
    g.addvar("cell.soma.v(.5)",2,1)                             // add the somatic voltage to 'g', color index 2 (red), brush index 1 (line width 1)
    g.size(0,tstop,-80,80)                                      // adjust the scale of the 'g'
}
	
// load external spike train
objref inputFile, inputSpikeTrain
inputFile	 	= new File()
inputSpikeTrain = new Vector() 
inputFile.ropen("input/inputSpikeTrain.txt")
inputSpikeTrain.scanf(inputFile)
inputFile.close()
    
// iClamp
//access cell.soma
//objref stimobj
//stimobj = new IClamp(0.5)
//stimobj.del = 500   // ms
//stimobj.dur = 1000  // ms
//stimobj.amp = 0.1   // nA
		
// create the extra input
create      myTRC									                    // Thalamic Relay Cell (dummy compartment)
strdef      cmd, tmpstr
objectvar   mystim[ntc], mysyn[ntc], mycon[ntc]
objref      tmpvec_i, tmpvec_v, veclist_i, veclist_v, mydist
mydist      = new Vector(ntc)
veclist_i   = new List()
veclist_v   = new List()
ii          = 0
forsec myloc_name {
    tmpvec_i            = new Vector()
    tmpvec_v            = new Vector()
    myTRC mystim[ii]	= new NetStim(0.5)						
    mystim[ii].number 	= 0						                        // ensure that the synapse is not spontaneously activated
    mysyn[ii] 			= new Exp2Syn(myloc_pos.x[ii])
    mysyn[ii].tau1 		= 0.3
    mysyn[ii].tau2 		= 1.7
    mysyn[ii].e 		= 0
    tmpvec_i.record(&mysyn[ii].i)
    veclist_i.append(tmpvec_i)
    tmpvec_v.record(&v(myloc_pos.x[ii]))                                // record the voltage in the corresponding compartment
    veclist_v.append(tmpvec_v)    
    mycon[ii] 		    = new NetCon(mystim[ii], mysyn[ii], 1, 0, gms)  // connect the TRC to the synapse
    mydist.x[ii]	    = distance(myloc_pos.x[ii])
    ii                  += 1
}
objref tmpvec_i, tmpvec_v                                               // to prevent careless use of tmpvec from damaging the last vector
    
// handlers
proc InitializeInputs(){
    for jj = 0, ntc-1 {
	    for ii = 0, inputSpikeTrain.size()-1 {mycon[jj].event(inputSpikeTrain.x[ii])}
    }
}   	
objref fih
fih = new FInitializeHandler(1,"InitializeInputs()")                    // create series of events to trigger the thalamocortical synapse

// create background activity
load_file("background.hoc")
load_file("thalamocortical background.hoc")   

// display the shape and extra synapse
objref shape
strdef distlbl
shape = new Shape(0)
shape.view(-200, -200, 400, 400, 900, 200, 300.48, 300.32)    
shape.show(0)                                                           // show diameter
//for i = 0, dendsegments-1 {
//    shape.point_mark(exsyn[i], 2, 4, 1000/exnetstim[i].interval)   
//    shape.point_mark(insyn[i], 3, 4, 1000/innetstim[i].interval)   
//    shape.point_mark(tcsyn[i], 5, 4, 1000/tcnetstim[i].interval)
//}
ii = 0
forsec myloc_name {
    shape.point_mark(mysyn[ii], 7, 4, 8)
    sprint(distlbl,"distance to soma %-4.1f um",mydist.x[ii])           // create label to print the distance on the shape plot
    shape.label(-180,170-(ii-1)*30,secname(),1,1,0,0,7)
    shape.label(-50,170-(ii-1)*30,distlbl,1,1,0,0,7)
    ii += 1
}
plot_idx = 3    
if (switch_plot == 0) {plot_idx = 1}      
pwm.paper_place(plot_idx,0,0,2)                                         // transfer the shape plot to the paper icon 
sprint(fname,"output/dataset#19/shape.eps") 
shape.observe(cell.dends)   
pwm.printfile(fname, 0, 0)
pwm.close(plot_idx)            	                                        // close the shape plot                        

// move all synaptic activity to the soma
objref vec_cond_e, vec_cond_i, vec_cond_tc
if (switch_soma > 0) {
    access cell.soma                                                    // access the soma
    for ii = 0, ntc-1 { mysyn[ii].loc(0.5) }                            // move the thalamocortical synapses to the soma
    for ii = 0, dendsegments-1 {
        exsyn[ii].loc(0.5)                                              // move all other synapses to the soma
        insyn[ii].loc(0.5)
        tcsyn[ii].loc(0.5)
        if (switch_soma == 2) {
            
            // OPTION #2 - Only one synapse with a higher rate                              
            exnetstim[ii].start = 2*tstop                               // set their start time to after the end of the simulation
            innetstim[ii].start = 2*tstop               
            tcnetstim[ii].start = 2*tstop               
        }       
    }
    if (switch_soma == 2) {
        
        // OPTION #2 - Only one synapse with a higher rate
        exnetstim[0].interval 	= 1000/(fe*Ne)                          // adjust the interval only for the first synapse
        innetstim[0].interval 	= 1000/(fi*Ni)
        tcnetstim[0].interval 	= 1000/(Ntc*ftc)
        exnetstim[0].start      = 0                                     // reset the start time of that synapse to 0
        innetstim[0].start      = 0
        tcnetstim[0].start      = 0
        exnetstim[0].number     = 100*tstop/exnetstim[0].interval                         
        innetstim[0].number     = 100*tstop/innetstim[0].interval  
        tcnetstim[0].number     = 100*tstop/innetstim[0].interval  
        
        // OPTION #2 - Record the generated conductance
        vec_cond_e      = new Vector()
        vec_cond_i		= new Vector()
        vec_cond_tc		= new Vector()
        vec_cond_e.record(&exsyn[0].g)
        vec_cond_i.record(&insyn[0].g)                           
        vec_cond_tc.record(&tcsyn[0].g)                           
    }
}

// new advance procedure
proc advance() {
    fadvance()
    if (switch_print == 1) {   
	    itot_na = 0
        itot_ca = 0 
        itot_ex = 0
        itot_tc = 0
        itot_ms = 0
        forall {		
		    for (x) {   
  	    	    if (x==0 || x ==1) { continue }
  	    	    read_compartment_currents(x)
            }
        }
        for ii = 0, dendsegments-1 {
            isyn    = exsyn[ii].i            
            vs      = v(exsyn[ii].get_loc())                            // get voltage at that location
		    itot_ex = itot_ex + isyn*(vs-ena)/(vs-exsyn[ii].e)*1/(1-ena/ek)
            isyn    = tcsyn[ii].i
            itot_tc = itot_tc + isyn*(vs-ena)/(vs-tcsyn[ii].e)*1/(1-ena/ek)    
            pop_section()
        }
        ii = 0
        forsec myloc_name {
            isyn    = mysyn[ii].i
            itot_ms = itot_ms + isyn*(v-ena)/(v-mysyn[ii].e)*1/(1-ena/ek)
            ii      += 1
        }    
    }
}	        
        
// run simulations
for idx = 0, gains.size()-1 {
	
	// set time reference for each run
	st = pc.time
		
	// set gain
	gain = gains.x(idx)
	if (gain < 10) {
		sprint(gainasastring,"0%-2.2f",gain)
	} else {
		sprint(gainasastring,"%-2.2f",gain)
	}
	    
	// update synaptic weights
    for ii = 0, ntc-1 {mycon[ii].weight = gms*gain}    
	rngseed	     = rngseed + 10*idx
    if (switch_scaling == 1) {
        for ii = 0, dendsegments-1 {
            tcnetstim[ii].seed(rngseed + ii)
		    tcnetcon[ii].weight = gtc*gain
	    }
    }       
	sprint(seedasastring,"%-d",rngseed)		        
		
	// terminal output
	printf("//\n")
	printf("//	Running simulation for gain = %-2.2f and scenario #0%d\n",gain,scenario)
	printf("//\n")		
	
	// run statement
	printf("//		Running simulation...\n")
	run()
		
	// output data
    if (switch_print == 1) {      
	    
        printf("//		Printing output...\n")	

	    sprint(fname,"output/dataset#19/%s_%s_%s_voltage.txt",prefix,gainasastring,seedasastring) 
	    outfile.wopen(fname)
        savevec.printf(outfile, "%g\n")
        outfile.close()
	
        sprint(fname,"output/dataset#19/%s_%s_%s_APs.txt",prefix,gainasastring,seedasastring)
        outfile.wopen(fname)	
        stvector.printf(outfile, "%g\n")
        outfile.close()		

        sprint(fname,"output/dataset#19/%s_%s_%s_currents.txt",prefix,gainasastring,seedasastring)
        outfile.wopen(fname)
        for i = 0, vec_sodium.size()-1 {
		    outfile.printf("%f\t", vec_sodium.x[i] )
            outfile.printf("%f\t", vec_calcium.x[i] )
            outfile.printf("%f\t", vec_ex.x[i] )
            outfile.printf("%f\t", vec_tc.x[i] )
            outfile.printf("%f\n", vec_ms.x[i] )
	    }
	    outfile.close()
            
        if (switch_soma == 2) {
            sprint(fname,"output/dataset#19/%s_%s_%s_%s_conductances.txt",prefix,myloc,gainasastring,seedasastring) 
            outfile.wopen(fname)
            for i = 0, vec_cond_e.size()-1 {
                outfile.printf("%f\t", vec_cond_e.x[i] )
                outfile.printf("%f\t", vec_cond_i.x[i] )
                outfile.printf("%f\n", vec_cond_tc.x[i] )
            }
            outfile.close()
        }
                
    }
		
	// print elapsed time and output frequency
	strdef strt
	eta = pc.time-st
	if (eta < 60) {
		etaout 	= eta
		strt 	= "seconds"
	} else {
		if (eta < 3600) {
			etaout 	= eta/60
			strt 	= "minutes"
		} else {
			etaout 	= eta/3600
			strt 	= "hours"
		}
	} 
	printf("//		Elapsed time = %-9.1f %s\n",etaout,strt)	
	printf("//		Output frequency = %-9.1f Hz\n",1000*stvector.size()/(tstop-tstart))		
		
}
// the end